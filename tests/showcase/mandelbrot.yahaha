// Code is this file is largely based on the tutorial
// "Kaleidoscope: Implementing a Language with LLVM"
// http://llvm.org/docs/tutorial/LangImpl06.html

var density := func(d) is
    if d > 16 then
        " ";
    else if d > 8 then
        "`";
    else if d > 4 then
        ".";
    else if d > 2 then
        "+";
    else if d > 1 then
        "*";
    else
        "#";
    end; end; end; end; end; // 5 nested else-if blocks
end;

var print_density := func(d) => print(density(d));

assert("#" = density(1));
assert("*" = density(2));
assert("+" = density(3));
assert("+" = density(4));
assert("." = density(5));
assert("`" = density(10));

// Determine whether the specific location diverges.
// Solve for z = z^2 + c in the complex plane.
var mandle_converger := func(real, imag, iters, creal, cimag) is
    if iters > 255 or (real*real + imag*imag > 4) then
        iters;
    else
        mandle_converger(
            real*real - imag*imag + creal,
            2*real*imag + cimag,
            iters+1,
            creal,
            cimag);
    end;
end;


// Return the number of iterations required for the iteration to escape
var mandle_converge := func(real, imag) =>
    mandle_converger(real, imag, 0, real, imag);


// Compute and plot the Mandelbrot set with the specified 2 dimensional range info.
var mandel_help := func(xmin, xmax, xstep, ymin, ymax, ystep) is
    var y := ymin; while y < ymax loop
        var x := xmin; while x < xmax loop
            print_density(mandle_converge(x, y));
            x := x + xstep;
        end;
        println();
        y := y + ystep;
    end;
end;


// mandel - This is a convenient helper function for plotting the Mandelbrot set
// from the specified position with the specified Magnification.
var mandel := func(realstart, imagstart, realmag, imagmag) =>
    mandel_help(
        realstart,
        realstart+realmag*78,
        realmag,
        imagstart,
        imagstart+imagmag*40,
        imagmag
    );

var separator := func() => print("\n\n");

mandel(-2.3, -1.3, 0.05, 0.07);
separator();
mandel(-2, -1, 0.02, 0.04);
separator();
mandel(-0.9, -1.4, 0.02, 0.03);
